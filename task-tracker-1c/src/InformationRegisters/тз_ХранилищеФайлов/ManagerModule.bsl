#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗаписатьДвоичныеДанные(Знач Файл, Знач ДвоичныеДанные) Экспорт
	
	Хеширование = Новый ХешированиеДанных(ХешФункция.SHA256);

	ЭтоПустыеДвоичныеДанные = (ДвоичныеДанные = Неопределено);
	Если ЭтоПустыеДвоичныеДанные Тогда
		ПустыеДвоичныеДанные = ПолучитьДвоичныеДанныеИзСтроки("");
		Хеширование.Добавить(ПустыеДвоичныеДанные);
		Размер = ПустыеДвоичныеДанные.Размер();
	Иначе
		Хеширование.Добавить(ДвоичныеДанные);
		Размер = ДвоичныеДанные.Размер();
	КонецЕсли;
	Хеш = ПолучитьBase64СтрокуИзДвоичныхДанных(Хеширование.ХешСумма);
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.тз_ХранилищеДвоичныхДанных");
		ЭлементБлокировки.УстановитьЗначение("Хеш", Хеш);
		Блокировка.Заблокировать();
		
		УдалитьДвоичныеДанные(Файл);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Хеш", Хеш);
		Запрос.УстановитьПараметр("Размер", Размер);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	тз_ХранилищеДвоичныхДанных.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.тз_ХранилищеДвоичныхДанных КАК тз_ХранилищеДвоичныхДанных
		|ГДЕ
		|	тз_ХранилищеДвоичныхДанных.Хеш = &Хеш
		|	И тз_ХранилищеДвоичныхДанных.Размер = &Размер";
		Выборка = Запрос.Выполнить().Выбрать();
		ХранилищеДвоичныхДанныхСсылка = Неопределено;
		Если Выборка.Следующий() Тогда
			ХранилищеДвоичныхДанныхСсылка = Выборка.Ссылка;
		Иначе
			ХранилищеДвоичныхДанныхОбъект = Справочники.тз_ХранилищеДвоичныхДанных.СоздатьЭлемент();
			ХранилищеДвоичныхДанныхОбъект.Размер = Размер;
			ХранилищеДвоичныхДанныхОбъект.Хеш = Хеш;
			ХранилищеДвоичныхДанныхОбъект.ДвоичныеДанные = ?(ЭтоПустыеДвоичныеДанные,
				Неопределено, Новый ХранилищеЗначения(ДвоичныеДанные, Новый СжатиеДанных(9)));
			ХранилищеДвоичныхДанныхОбъект.Записать();
			ХранилищеДвоичныхДанныхСсылка = ХранилищеДвоичныхДанныхОбъект.Ссылка;
		КонецЕсли;
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.Файл = Файл;
		Запись.ХранилищеДвоичныхДанных = ХранилищеДвоичныхДанныхСсылка;
		Запись.Записать(Ложь);
		
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура УдалитьДвоичныеДанные(Файл) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Файл", Файл);
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	тз_ХранилищеФайлов.ХранилищеДвоичныхДанных КАК ХранилищеДвоичныхДанных,
		|	тз_ХранилищеФайлов.ХранилищеДвоичныхДанных.Хеш КАК Хеш
		|ИЗ
		|	РегистрСведений.тз_ХранилищеФайлов КАК тз_ХранилищеФайлов
		|ГДЕ
		|	тз_ХранилищеФайлов.Файл = &Файл";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.тз_ХранилищеДвоичныхДанных");
			ЭлементБлокировки.УстановитьЗначение("Хеш", Выборка.Хеш);
			Блокировка.Заблокировать();
			
			Запись = СоздатьМенеджерЗаписи();
			Запись.Файл = Файл;
			Запись.Удалить();
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("тз_ХранилищеДвоичныхДанных", Выборка.ХранилищеДвоичныхДанных);
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА КАК Проверка
			|ИЗ
			|	РегистрСведений.тз_ХранилищеФайлов КАК тз_ХранилищеФайлов
			|ГДЕ
			|	тз_ХранилищеФайлов.ХранилищеДвоичныхДанных = &ХранилищеДвоичныхДанных";
			Если Запрос.Выполнить().Пустой() Тогда
				СпрОбъект = Выборка.ХранилищеДвоичныхДанных.ПолучитьОбъект();
				СпрОбъект.ОбменДанными.Загрузка = Истина;
				СпрОбъект.Удалить();
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();	
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
