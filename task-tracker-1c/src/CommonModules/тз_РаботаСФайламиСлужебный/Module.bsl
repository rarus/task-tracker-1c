#Область ПрограммныйИнтерфейс

Функция КонтекстОбновленияФайла(ПрисоединенныйФайл, ДанныеФайла, СсылкаНаФайл = Неопределено, ТипХраненияФайла = Неопределено) Экспорт
	Контекст = Новый Структура;
	Контекст.Вставить("ИзменяемыеРеквизиты", Новый Структура);
	Контекст.Вставить("СтарыйПутьКФайлу", "");
	Контекст.Вставить("ПараметрыДобавленияФайла", тз_РаботаСФайламиВТомахСлужебный.ПараметрыДобавленияФайла());
	Контекст.Вставить("ЭтоНовый", Ложь);
	Контекст.Вставить("ПрисоединенныйФайл", Неопределено);
	ТипПараметраФайла = ТипЗнч(ПрисоединенныйФайл);
	Если НЕ тз_ОбщегоНазначения.ЭтоСсылка(ТипПараметраФайла)  Тогда
		Контекст.ЭтоНовый =  ПрисоединенныйФайл.ЭтоНовый();
	КонецЕсли;
	Контекст.ПрисоединенныйФайл = ?(ЗначениеЗаполнено(СсылкаНаФайл), СсылкаНаФайл, ПрисоединенныйФайл.Ссылка);
	
	ЗаполнитьЗначенияСвойств(Контекст.ПараметрыДобавленияФайла, ПрисоединенныйФайл);
	Контекст.ПараметрыДобавленияФайла.ТипХраненияФайла = ТипХраненияФайла;
	
	//Если ТипЗнч(Контекст.ПрисоединенныйФайл) = Тип("СправочникСсылка.ВерсииФайлов") Тогда
	//	Контекст.ПараметрыДобавленияФайла.ВладелецФайла = ПрисоединенныйФайл.Владелец.ВладелецФайла;
	//КонецЕсли;
	Если ТипЗнч(ДанныеФайла) = Тип("Строка") И ЭтоАдресВременногоХранилища(ДанныеФайла) Тогда
		ДанныеФайла = ПолучитьИзВременногоХранилища(ДанныеФайла);
	КонецЕсли;
	
	Контекст.Вставить("ДанныеФайла", ДанныеФайла);
	Возврат Контекст;
КонецФункции

Функция МенеджерФайлов(ПрисоединенныйФайл) Экспорт
	Возврат МенеджерФайловПоТипу(ПрисоединенныйФайл.ТипХраненияФайла);
КонецФункции

Функция МенеджерФайловПоТипу(ТипХраненияФайла) Экспорт
	Если ТипХраненияФайла = Перечисления.тз_ТипыХраненияФайлов.ВТомахНаДиске Тогда
		Возврат тз_РаботаСФайламиВТомахСлужебный;
	Иначе
		Возврат тз_РаботаСФайламиСлужебный;
	КонецЕсли;
КонецФункции

Процедура ПередОбновлениемДанныхФайла(Контекст) Экспорт
	Возврат; // Не используется
КонецПроцедуры

Процедура ПередЗаписьюДанныхФайла(Контекст, ПрисоединенныйФайлОбъект) Экспорт
	Возврат; // Не используется
КонецПроцедуры

Процедура ПриОбновленииДанныхФайла(Контекст, ПрисоединенныйФайл) Экспорт
	ЗаписатьФайлВИнформационнуюБазу(ПрисоединенныйФайл, Контекст.ДанныеФайла);
КонецПроцедуры

Процедура ПослеОбновленияДанныхФайла(Контекст, Успешно) Экспорт
	Возврат; // Не используется
КонецПроцедуры

Процедура ЗаписатьФайлВИнформационнуюБазу(Знач ПрисоединенныйФайл, Знач ДвоичныеДанные) Экспорт
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	
	РегистрыСведений.тз_ХранилищеФайлов.ЗаписатьДвоичныеДанные(ПрисоединенныйФайл, ДвоичныеДанные);
	
КонецПроцедуры

Функция ФайлОбъект(Знач ПрисоединенныйФайл) Экспорт
	
	ФайлОбъект = Неопределено;
	МетаданныеОбъектаФайла = Метаданные.НайтиПоТипу(ТипЗнч(ПрисоединенныйФайл));
	
	// Это справочник файлов.
	Если тз_ОбщегоНазначения.ЕстьРеквизитОбъекта("ВладелецФайла", МетаданныеОбъектаФайла) Тогда
		ЕстьТекущаяВерсия = Ложь;
		Если тз_ОбщегоНазначения.ЕстьРеквизитОбъекта("ТекущаяВерсия", МетаданныеОбъектаФайла) Тогда
			ТекущаяВерсия = тз_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрисоединенныйФайл, "ТекущаяВерсия"); 
			ЕстьТекущаяВерсия = ЗначениеЗаполнено(ТекущаяВерсия); 
		КонецЕсли;	
		Если ЕстьТекущаяВерсия Тогда // С возможностью хранить версии
			ИменаРеквизитов = "Ссылка,ТипХраненияФайла,Наименование,Расширение,Том,ПутьКФайлу,ПометкаУдаления";
			Если МетаданныеОбъектаФайла.Иерархический Тогда
				ИменаРеквизитов = ИменаРеквизитов + "," + "ЭтоГруппа";
			КонецЕсли;
			ФайлОбъект = тз_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущаяВерсия, ИменаРеквизитов);
			ФайлОбъект.Вставить("ВладелецФайла", тз_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПрисоединенныйФайл, "ВладелецФайла"));
			Если Не МетаданныеОбъектаФайла.Иерархический Тогда
				ФайлОбъект.Вставить("ЭтоГруппа", Ложь);
			КонецЕсли;
		Иначе // Без возможности хранить версии
			ИменаРеквизитов = "Ссылка,ТипХраненияФайла,ВладелецФайла,Наименование,Расширение,Том,ПутьКФайлу,ПометкаУдаления"; 
			Если МетаданныеОбъектаФайла.Иерархический Тогда
				ИменаРеквизитов = ИменаРеквизитов + "," + "ЭтоГруппа";
			КонецЕсли;
			ФайлОбъект = тз_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, ИменаРеквизитов);
			Если Не МетаданныеОбъектаФайла.Иерархический Тогда
				ФайлОбъект.Вставить("ЭтоГруппа", Ложь);
			КонецЕсли;
		КонецЕсли;
	// Это справочник версий файлов.
	ИначеЕсли тз_ОбщегоНазначения.ЕстьРеквизитОбъекта("РодительскаяВерсия", МетаданныеОбъектаФайла) Тогда
		ФайлОбъект = тз_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрисоединенныйФайл, 
			"Ссылка,ТипХраненияФайла,Наименование,Расширение,Том,ПутьКФайлу,Владелец,ПометкаУдаления");
		ФайлОбъект.Вставить("ВладелецФайла",
			тз_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФайлОбъект.Владелец, "ВладелецФайла"));
		ФайлОбъект.Удалить("Владелец");
		ФайлОбъект.Вставить("ЭтоГруппа", Ложь);
	КонецЕсли;
	
	Возврат ФайлОбъект;
	
КонецФункции

#КонецОбласти